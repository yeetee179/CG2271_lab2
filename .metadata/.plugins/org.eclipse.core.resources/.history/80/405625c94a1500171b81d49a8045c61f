#include <Arduino.h>
#include <avr/io.h>
#include <FreeRTOS.h>
#include <task.h>
#include <semphr.h>

#define STACK_SIZE 200

SemaphoreHandle_t semaphoreBinary;

QueueHandle_t queue;

void producer(void *p)
{
	while(1){
		if(xSemaphoreTake(semaphoreBinary, (TickType_t) 0)){
			int potentioValue = analogRead(0);
			xQueueSendToBack(queue, &potentioValue, (TickType_t) 5000);
		}
		else{
			vTaskDelay(1);
		}
	}
}

void consumer(void *p)
{
	TickType_t wakeTime = xTaskGetTickCount();
	while(1){
		int potentioValue;
		if(xQueueReceive(queue, &potentioValue, (TickType_t) 0)){
			Serial.println(potentioValue);
		}
		vTaskDelayUntil(&wakeTime, 5000);
	}
}

void producerISR()
{
	static unsigned long previousInterruptTime = 0;
	unsigned long currentInterruptTime = millis();

	if(currentInterruptTime - previousInterruptTime > 100){
		xSemaphoreGiveFromISR(semaphoreBinary, NULL);
		previousInterruptTime = currentInterruptTime;
	}
}

void setup() {
	Serial.begin(115200);

	attachInterrupt(digitalPinToInterrupt(3), producerISR, RISING);

	semaphoreBinary = xSemaphoreCreateBinary();

	queue = xQueueCreate(10, sizeof(int));
}

void loop() {
	/* create two tasks one with higher priority than the other */
	xTaskCreate(producer, "Producer", STACK_SIZE, NULL, 1, NULL);
	xTaskCreate(consumer, "Consumer", STACK_SIZE, NULL, 1, NULL);

	/* start scheduler */
	vTaskStartScheduler();
}

